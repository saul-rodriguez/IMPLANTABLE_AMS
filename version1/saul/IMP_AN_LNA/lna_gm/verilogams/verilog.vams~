//Verilog-AMS HDL for "IMP_AN_LNA", "lna_gm" "verilogams"

/*
 *  Variable gain Transconductance Low Noise Amplifier  
 *  Author: Saul Rodriguez
 *  Date: 2015-06-10
 *
 *  Description:
 *  This is a differential variable GM stage provides 3 gains
 *  which are controlled by the digital inputs G1-G0:
 *   G0  Gain
 *    0   6.66666 uS
 *    1   20 uS
 */


`include "constants.vams"
`include "disciplines.vams"

module lna_gm ( bias_CMFB, bias_in, G0, IoutN, IoutP, VIN_N, VIN_P, vdda, vddd,
vssa, vssd, vsub );

  input vsub;
  output IoutP;
  input G0;
  input VIN_P;
  input VIN_N;
  input vssd;
  input vdda;
  output IoutN;
  output bias_in;
  input vssa;
  input vddd;
  output bias_CMFB;

  logic G0;
  electrical VIN_N, VIN_P, IoutN, IoutP, vssa;
  electrical bias_CMFB;

  parameter real vcm = 0.9; //Common mode voltage
  parameter real Av = 20000; //DC voltage Gain 
  parameter real I_bias_CMFB = 0.5u;
  parameter real GM0 = 6.66666u;

  real GM; //Transconductance
  real Gout, Gout_se, vodif; 

  integer analog_state = 0; // Used to communicate between dig and analog!


  // Digital control block
  always @(G0)
  begin
	analog_state = 0;
	if (G0 == 1) begin
		analog_state = 1;
	end
	
	if (analog_state == 0) begin
		GM = GM0;
	end else begin 
		GM = 3*GM0;
	end

	Gout=GM/Av;  //Gout differential 
	Gout_se = 2*Gout; //Gout single ended 	

  end


  // Analog block
  analog begin
  	
	vodif = Av*V(VIN_P,VIN_N); // Differential gain (DC)
	
    //Amplifier Ouput current
	I(IoutP,vssa) <+ (V(IoutP,vssa)-(vcm+vodif/2))*Gout_se;
	I(IoutN,vssa) <+ (V(IoutN,vssa)-(vcm-vodif/2))*Gout_se;
	
	//Biasing for other stages	
    I(bias_CMFB) <+ -I_bias_CMFB;

  end 




endmodule
